plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'aikar-repo'
        url = 'https://repo.aikar.co/content/groups/aikar/'
    }
    maven {
        name = 'essx-repo'
        url = 'https://ci.ender.zone/plugin/repository/everything/'
    }
}

dependencies {
    compileOnly 'org.bukkit:bukkit:1.15.2-R0.1-SNAPSHOT'

    compileOnly 'net.ess3:EssentialsX:2.17.2'

    implementation 'co.aikar:idb-core:1.0.0-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:2.4.1'
    runtimeOnly 'org.xerial:sqlite-jdbc:3.30.1'
    runtimeOnly 'mysql:mysql-connector-java:5.1.48'

    testImplementation 'junit:junit:4.12'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {
    minimize()
    relocate 'co.aikar.idb', 'io.github.essentialsx.cmiimporter.idb'
    relocate 'com.zaxxer.hikari', 'io.github.essentialsx.cmiimporter.hikari'
}

build.dependsOn shadowJar
